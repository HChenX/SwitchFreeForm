plugins {
    alias(libs.plugins.android.application)
}

android {
    def apk = "SwitchFreeForm"

    Properties properties = new Properties()
    InputStream inputStream = project.rootProject.file('local.properties').newDataInputStream()
    properties.load(inputStream)

    def keyKeyStoreFile = file(properties.getProperty('keyStoreFile'))
    def keyKeyStorePassword = properties.getProperty('keyStorePassword')
    def keyKeyAlias = properties.getProperty('keyAlias')
    def keyKeyAliasPassword = properties.getProperty('keyAliasPassword')

    signingConfigs {
        release {
            storeFile keyKeyStoreFile
            storePassword keyKeyStorePassword
            keyAlias keyKeyAlias
            keyPassword keyKeyAliasPassword
            enableV2Signing true
            enableV3Signing true
            enableV4Signing true
        }
    }

    namespace 'com.hchen.switchfreeform'
    compileSdk 35

    defaultConfig {
        applicationId "com.hchen.switchfreeform"
        minSdk 34
        targetSdk 35
        //noinspection HighAppVersionCode
        versionCode 2025021001
        versionName "1.2"

        signingConfig signingConfigs.release
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    android {
        applicationVariants.configureEach { variant ->
            variant.outputs.configureEach { output ->
                outputFileName = "${apk}-v${variant.versionName}(${variant.versionCode})_${variant.buildType.name}.apk"
            }
        }
    }

    buildFeatures {
        buildConfig true
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'
    implementation 'com.github.HChenX:HookTool:v.1.2.2'

    compileOnly(project(':miui'))
}